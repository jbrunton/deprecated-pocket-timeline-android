apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

String java8 = getJavaVersion(8)
String java6 = getJavaVersion(6)
retrolambda {
    jdk java8 //have tried with JAVA8_HOME
    oldJdk java6 //have tried with JAVA6_HOME
    javaVersion JavaVersion.VERSION_1_6 //have matched the above with VERSION_1_6
}

// AS doesn't seem to pick up environment variables. See:
// https://github.com/evant/gradle-retrolambda/issues/61#issuecomment-66581029
String getJavaVersion(Integer v) {
    def sout = new StringBuffer()
    def proc = "/usr/libexec/java_home -v 1.$v".execute()
    proc.consumeProcessOutput(sout, new StringBuffer())
    proc.waitForOrKill(1000)
    return sout.toString().replace("\n", "").replace("\r", "")
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    // temporary workaround for testing with Robolectric
    // see: https://github.com/robolectric/robolectric/issues/1862
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.jbrunton.pockettimeline"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    testCompile 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'joda-time:joda-time:2.8.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile ('com.squareup.assertj:assertj-android:1.1.0') {
        exclude module: 'support-annotations'
    }

}
